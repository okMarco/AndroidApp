apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def getAppVersionCode(shortVersionCode) {
    String versionName = VERSIOM_NAME
    String code = ""
    if (shortVersionCode) {
        code = versionName.replaceAll("\\.", "")
    } else {
        String[] version = versionName.split("\\.")
        version.each {
            while (it.length() < 3) {
                it = "0" + it
            }
            code += it
        }
    }
    println code
    return Integer.valueOf(code)
}

ext.VERSIOM_NAME = '3.3.2'

android {
    signingConfigs {
        debug {
            keyAlias 'HoChan'
            keyPassword '088210'
            storeFile file('/Users/chenzhendong/Documents/Workspace/Tunlodr发布版本/hochan.jks')
            storePassword '088210'
        }
        release {
            keyAlias 'HoChan'
            keyPassword '088210'
            storeFile file('/Users/chenzhendong/Documents/Workspace/Tunlodr发布版本/hochan.jks')
            storePassword '088210'
        }
    }
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.hochan.coldsoup"
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode getAppVersionCode(false)
        versionName VERSIOM_NAME
        flavorDimensions "versionCode"

        ndk {
            // 设置支持的SO库架构
            abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a'
        }

        multiDexEnabled true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {
        // 测试环境的包
        dev {}
        app {}
    }

    dataBinding {
        enabled = true
    }
    //为了解决部分第三方库重复打包了META-INF的问题
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            // 获取渠道
            def flavor = variant.productFlavors[0]
            def flavorName
            if (flavor == null) {
                flavorName = "defaultName"
            } else {
                flavorName = flavor.name
            }

            def isRelease = variant.buildType.name.contains('release')
            // 非release为3位code，用于区分fabric线上和dev
            // variant.mergedFlavor.versionCode 表示修改打包时versionCode的值
            variant.mergedFlavor.versionCode = getAppVersionCode(!isRelease)
            outputFileName = "com.hochan.coldsoup_" + "${flavorName}_${variant.buildType.name}_v${variant.versionName}_${variant.versionCode}.apk"
            println outputFileName
        }
    }
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.tumblr:jumblr:0.0.11'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    implementation 'com.github.castorflex.smoothprogressbar:library-circular:1.3.0'
    implementation 'com.facebook.rebound:rebound:0.3.8'
    implementation 'com.hoooooo:arrow:1.0.0'
    implementation 'org.jsoup:jsoup:1.9.2'
    implementation 'com.liulishuo.filedownloader:library:1.6.4'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation('com.github.bumptech.glide:recyclerview-integration:4.5.0') {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-navi:2.1.0'
    testCompile 'junit:junit:4.12'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0'
    // ViewModel and LiveData
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    // alternatively, just ViewModel
    implementation 'android.arch.lifecycle:viewmodel:1.1.1'
    // alternatively, just LiveData
    implementation 'android.arch.lifecycle:livedata:1.1.1'
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"
    // Room (use 1.1.0-alpha2 for latest alpha)
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    // Paging
    implementation 'android.arch.paging:runtime:1.0.1'
    // Test helpers for LiveData
    testImplementation 'android.arch.core:core-testing:1.1.1'
    // Test helpers for Room
    testImplementation 'android.arch.persistence.room:testing:1.1.1'
    // RxJava support for Room (use 1.1.0-alpha1 for latest alpha)
    implementation 'android.arch.persistence.room:rxjava2:1.1.1'
    // ReactiveStreams support for LiveData
    implementation 'android.arch.lifecycle:reactivestreams:1.1.1'
    implementation 'com.google.android:flexbox:1.0.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    implementation 'com.google.firebase:firebase-ads:17.2.0'
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation('com.devbrackets.android:exomedia:4.2.1') {
        exclude group: 'com.android.support'
    }
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.0.5.1'
    implementation "com.google.android.exoplayer:extension-okhttp:2.8.1"

    implementation 'com.danikula:videocache:2.7.1'
}


apply plugin: 'com.google.gms.google-services'
